apply plugin: 'findbugs'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'maven'

group = 'org.gradle.plugins'
version = '1.0.0-SNAPSHOT'

description = "Provides utility tasks to work with dependencies like analyzing, resolving and updating."

sourceCompatibility = '1.8'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

// Disable strict doclint - http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

ext {
    jacocoVersion = '0.7.5.201505241946'
}

findbugs {
    ignoreFailures = true
    reportLevel = 'low'
}

findbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ow2.asm:asm:5.1'

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.2.2'
    testCompile 'junit:junit-dep:4.12'
    testCompile 'org.objenesis:objenesis:1.2'
}

test {
    filter {
        includeTestsMatching "*Spec"
    }
}

jacoco {
    toolVersion = jacocoVersion
}

/*
 * Set the Jacoco generate report task to not cache and set it to
 * run automatically after the "test" task.
 */
project.tasks.jacocoTestReport.outputs.upToDateWhen { false }
test.finalizedBy(project.tasks.jacocoTestReport)